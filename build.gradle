plugins {
    id "idea"
    id "java"
    id "application"
    id "antlr"
}

version = '0.1.0'
group = 'org.github.totopoloco'

application {
    mainClass.set('at.mavila.antlr.Main')
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.1'
    implementation 'org.antlr:antlr4-runtime:4.13.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.slf4j:slf4j-simple:1.7.25'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

// Antlr
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += [ "-visitor", 
                   "-long-messages",
                   "-package", "at.mavila.antlr" ]
    outputDirectory = file("${projectDir}/src/generated/java")
}

clean {
    delete file("${projectDir}/src/generated")
}

sourceSets {
    generated{
        java.srcDir "${projectDir}/src/generated/java/"
    }

    main {
        java.srcDirs += "${projectDir}/src/generated/java/"
    }
}

// Intellij
idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/java")
        generatedSourceDirs += file("${projectDir}/src/generated/java")
    }
}